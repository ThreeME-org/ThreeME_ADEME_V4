##### Producer

## This section provides the equations defining the producer behaviour.

# Most equation are behavioral equations. They are not used to calibrate the initial value of variables. They may be inverted to calibrate a parameter.

#### Production factors

##! Production of commodity $c$ by sector $s$
Y[c, s] = PhiY[c, s] * YQ[c] if Y[c, s] <> 0
## We assume that each sector $s$ may produce more than one commodity $c$. Therefore the production $Y$ of commodity $c$ by sector $s$ depends on the parameter $\varphi^{Y}_{c, s}$ which represents the share of sector $s$ in the total production of commodity $c$. \\

##! Demand for production factor $f$ by sector $s$
d(log(F_n[f, s]))  = d(log(Y[s])) - d(log(PROG[f, s])) + d(SUBST_F[f, s])                                                 if F_n[f, s] <> 0

##! Notional substitution between input $f$ and the other inputs $ff$
d(SUBST_F_n[f, s]) = sum(- ES[f, ff, s] * phi[ff, s]{-1} * d(log(C[f, s]/PROG[f, s]) - log(C[ff, s]/PROG[ff, s])) on ff)  if F_n[f, s] <> 0

##! Cost share of input $f$ for sector $s$
phi[f, s]          = (C[f, s] * F_n[f, s]) / (sum(C[ff, s] * F_n[ff, s] on ff))


##! Aggregate production factors $f$
F[f] = sum(F[f, s] on s)

##! Investment in commodity $c$ by sector $s$
d(log(I[c, s])) = d(log(IA[s]))                                                                                           if I[c, s]    <> 0
## For a given sector, we assume that the investment structure is fixed over time. In other words, the investment good is a composite of several commodities in fixed proportion. \\

##! Energy consumption $ce$ of sector $s$
d(log(CI[ce, s]))    = d(log(F[E, s])) + d(SUBST_CI[ce,s])                                                                if CI[ce, s]  <> 0

##! Notional substitution between energy commodity $ce$ and the other energy commodities $cee$ in the sector $s$
d(SUBST_CI_n[ce, s]) = sum(- ES_NRJ[ce, cee, s] * phi[E, cee, s]{-1} * d(log(PCI[ce, s]) - log(PCI[cee, s])) on cee)      if CI[ce, s]  <> 0

##! Share of energy $ce$ into the total energy use of sector $s$
phi[E, ce, s] = (PCI[ce, s]*CI[ce, s]) / (sum(PCI[cee, s]*CI[cee, s] on cee) + eps)


##! Material consumption $cmo$ of sector $s$
d(log(CI[cmo, s])) = d(log(F[MAT, s]))                                                                                    if CI[cmo, s] <> 0
## We assume that intermediary consumption that are not transport or energy commodities are not substitutables (Leontief technology).  \\

##! Transport demand of sector $s$
d(log(TRSP[s])) = d(log(F[MAT, s]))                                                                                       if TRSP[s]    <> 0
#  We assume no substitution between transport intermediaries and the other material intermediaries. \\

##! Demand for transport commodity $ct$ by sector $s$
d(log(CI[ct, s])) = d(log(TRSP[s])) + d(SUBST_CI[ct,s])                                                                   if CI[ct, s]  <> 0

##! Notional substitution between the transport $ct$ and the other transports $mt$ in the sector $s$
d(SUBST_CI_n[ct, s]) = sum(- ES_TRSP[ct, ctt, s] * phi_TRSP[ctt, s]{-1} * d(log(PCI[ct, s]) - log(PCI[ctt, s])) on ctt)   if CI[ct, s]  <> 0

##! Share for transport $ct$ into the total transport use of sector $s$
phi_TRSP[ct, s] = (PCI[ct, s]*CI[ct, s]) / (sum(PCI[ctt, s]*CI[ctt, s] on ctt))

##! Technical progress of the production factor $f$ in sector $s$
PROG[f, s] = PROG[f, s]{-1} * (1 + GR_PROG[f, s])

##! Energy efficiency gains in sector $s$
GR_PROG[E, s] = GR_PROG_exo[E, s] + RHO_PROG_E_PE * (log(PE[s]) - log(P) > 0) * d(log(PE[s])-log(P)) 
## This specification states that the productivity gain of the energy input in sector $s$ depends on an exogenous trend and an endogenous price-induced component. Energy efficiency gains increase when the real energy price increases. This effect is assumed as irreversible: a decrease in the real energy price does not lead to a decrease in energy efficiency gains. \\

# Doute sur le specification ci-dessus qui reprend celle existante:
# GR_PROG_E[s] =  alpha_PROG_E[s] + alpha_PROG_E_PE*((log(PE[s])- log(PCH)>0)*d(log(PE[s])-log(PCH)))
# On a une variation qui affecte un niveau. L'effet doit être faible.
# Je serais tenter de mettre plutot (à discuter!!!):
# GR_PROG[E, s] = @elem(GR_PROG[E, s], %baseyear) + RHO_PROG_E_PE * (log(PE[s]) - log(P) > 0) * (log(PE[s])-log(P))
